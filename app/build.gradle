plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.google.dagger.hilt.android'
}

def keystorePropertiesFile = rootProject.file("key.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    namespace 'com.maary.liveinpeace'
    compileSdk 35

    defaultConfig {
        applicationId "com.maary.liveinpeace"
        minSdk 31
        targetSdk 35
        versionCode 5
        versionName "2025.04.05-01"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
        dataBinding false
        compose true
    }
    splits {
        // Configures multiple APKs based on ABI.
        abi {
            // Enables building multiple APKs per ABI.
            enable true

            // Specifies the ABIs that Gradle should create APKs for.
            // This example creates APKs for the armeabi-v7a, arm64-v8a, x86, and x86_64 ABIs.
            // Other ABIs that you could include are: mips, mips64, and ppc64.
            universalApk false
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            // 查找 ABI 过滤器
            def abiFilter = output.filters.find { it.filterType == "ABI" }
            def abi = abiFilter?.identifier

            def apkName = "LiveInPeace-${abi}.apk"
            if (abi == "x86_64") {
                apkName = "LiveInPeace-x64.apk"
            }
            output.outputFileName = apkName
        }
    }


}

dependencies {

    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    implementation 'androidx.activity:activity-ktx:1.10.1'
    implementation 'androidx.databinding:databinding-runtime:8.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.1'
    implementation 'androidx.activity:activity-compose:1.10.1'
    implementation platform('androidx.compose:compose-bom:2025.06.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3:1.4.0-alpha15'
    androidTestImplementation platform('androidx.compose:compose-bom:2025.06.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.9.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.9.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.9.1"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.9.1'

    implementation "com.google.dagger:hilt-android:2.56.2"
    ksp "com.google.dagger:hilt-compiler:2.56.1"


    implementation "androidx.room:room-runtime:2.7.1"
    annotationProcessor "androidx.room:room-compiler:2.7.1"
    implementation 'androidx.room:room-ktx:2.7.1'
    ksp "androidx.room:room-compiler:2.7.1"

    implementation "androidx.datastore:datastore-preferences:1.2.0-alpha02"

    implementation("com.google.accompanist:accompanist-permissions:0.37.3")
    implementation "androidx.compose.material:material-icons-extended:1.7.8"

    implementation "androidx.work:work-runtime-ktx:2.10.1"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}